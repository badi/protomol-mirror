from __future__ import with_statement
import os
import sys


# Command Line Options
opts = Options('options.py')
Export('opts')

opts.AddOptions(
    EnumOption('mode', 'Set build mode', 'debug',
               allowed_values = ('debug', 'release')),
    BoolOption('optimize', 'Set to 1 to force optimizations', 0),
    BoolOption('debug', 'Set to 1 to force debug options', 0),
    BoolOption('strict', 'Set to 0 to disable strict options', 1),
    BoolOption('depends', 'Set to 1 to output dependency files', 0),
    BoolOption('fah', 'Set to 1 to build library for Folding@Home', 0),
    EnumOption('compiler', 'Select compiler', 'default',
               allowed_values = ('default', 'gnu', 'intel', 'mingw', 'msvc',
                                 'linux-mingw', 'aix', 'posix', 'hp', 'sgi',
                                 'sun')),
    )


# Create environment and import external PATH
env = Environment(options = opts, ENV = os.environ)
Export('env')

# Configure
conf = Configure(env)
Export('conf')


# Import subdir build files
subsrc = SConscript('protomol/SConscript')


# Generate Help
Help(opts.GenerateHelpText(env))


# Get options
mode = env.get('mode', 'debug')

if env.has_key('optimize'): optimize = int(env['optimize'])
else: optimize = mode == 'release'

if env.has_key('debug'): debug = int(env['debug'])
else: debug = mode == 'debug'

strict = int(env.get('strict', 1))
compiler = env.get('compiler', 0)
depends = int(env.get('depends', 0))
fah = int(env.get('fah', 0))


# Select compiler
if compiler:
    if compiler == 'gnu':
        Tool('gcc')(env)
        Tool('g++')(env)

    elif compiler == 'intel':
        Tool('intelc')(env)
        env['ENV']['INTEL_LICENSE_FILE'] = os.environ.get("INTEL_LICENSE_FILE",
                                                          '')
    elif compiler == 'linux-mingw':
        env.Replace(CC = 'i586-mingw32msvc-gcc')
        env.Replace(CXX = 'i586-mingw32msvc-g++')
        env.Replace(RANLIB = 'i586-mingw32msvc-ranlib')
        env.Replace(PROGSUFFIX = '.exe')

    elif compiler == 'posix':
        Tool('cc')(env)
        Tool('cxx')(env)
        Tool('link')(env)
        Tool('ar')(env)
        Tool('as')(env)

    elif compiler in Split('hp sgi sun aix'):
        Tool(compiler + 'cc')(env)
        Tool(compiler + 'c++')(env)
        Tool(compiler + 'link')(env)

        if compiler in Split('sgi sun'):
            Tool(compiler + 'ar')(env)

    elif compiler != 'default':
        Tool(compiler)(env)

print "Compiler: " + env['CC']

# Options
if env['CC'] == 'cl':
    env.Append(CCFLAGS = '-EHsc')

# Debug flags
if debug:
    if env['CC'] == 'cl':
        env.Append(CCFLAGS = '-W1 /MDd')
    elif env['CC'] == 'gcc':
        env.Append(CCFLAGS = '-g -Wall')
        if strict: env.Append(CCFLAGS = '-Werror')

    env.Append(CPPDEFINES = ['DEBUG'])


# Optimizations
if optimize:
    if env['CC'] in ['icc', 'icpc']:
        env.Append(CCFLAGS = '-O -finline-functions -funroll-loops')
    elif env['CC'] == 'gcc':
        env.Append(CCFLAGS =
                   '-O9 -ffast-math -finline-functions -funroll-loops')


# Dependency files
if depends:
    env.Append(CCFLAGS = '-MMD -MF ${TARGET}.d')

# Include protomol
env.Append(CPPPATH = '#')


# config.h
REV="unknown"
REPO="unknown"
with open('.svn/entries') as f:
    i = 0
    for line in f:
        i = i + 1
        if i > 5: break
        if i == 4: REV = line.strip()
        if i == 5: REPO = line.strip()
print Platform()
execfile('tags.py')
computed_tags = [
    ['REVISION', REV],
    ['SOURCE_REPO', '\n' + REPO],
    ['COMPILER', env.subst('$CXX')],
    ['COMPILER_VERSION', env.subst('$CXXVERSION')],
    ['COMPILER_FLAGS', env.subst('$CXXFLAGS $_CPPDEFFLAGS')],
    ['COMPILER_LIBS', env.subst('$LIBS')],
    ['BUILT_BY', os.environ.get('USER', '')],
    ['PLATFORM', env.subst('$PLATFORM')],
]

with open('protomol/package.h', 'w') as f:
    f.write('#ifndef PROTOMOL_PACKAGE_H\n')
    f.write('#define PROTOMOL_PACKAGE_H\n\n')
    f.write('/************************************************************\n')
    f.write(' * NOTE: This is an autogenerated file.  Please do not edit *\n')
    f.write(' *       directly or check in to revision control.          *\n')
    f.write(' ************************************************************/\n')
    f.write('\n')

    for tag in tags + computed_tags:
        f.write('#define PACKAGE_' + tag[0] + ' ')
        if tag[1].find('\n') != -1:
            i = 0
            for l in tag[1].split('\n'):
                if l != '':
                    i = i + 1
                    if i > 1: f.write('\\n"')
                    f.write('\\\n  "' + l)
            f.write('"\n')
        else:
            f.write('"' + tag[1] + '"\n')

    f.write('\n#endif // PROTOMOL_PACKAGE_H\n')
    env.Append(CPPDEFINES = ['HAVE_PACKAGE_H'])


libname = 'protomol'
libsrc = []

# Add libprotomol
env.Append(LIBPATH = '.')
env.Append(LIBS = 'protomol')


# libFAH
have_libfah=0
if os.environ.has_key('LIBFAH_HOME'):
    env.Append(CPPPATH = [os.environ['LIBFAH_HOME']])
    env.Append(LIBPATH = [os.environ['LIBFAH_HOME']])
if conf.CheckLib('fah'):
    env.Append(CPPDEFINES = ['HAVE_LIBFAH'])
    have_libfah=1

# pthreads
if env['PLATFORM'] != 'win32' and have_libfah:
    if not conf.CheckLib('pthread'):
        print ('Need libpthreads for Folding@Home GUI server on non-win32 ' +
               'platforms')
        Exit(1)

# Folding@Home library version
if fah:
    if not conf.CheckCXXHeader('fah/core/ChecksumDevice.h'):
        print 'Folding@Home needed please set LIBFAH_HOME'
        Exit(1)
    # Look for boost iostreams
    env.Append(CPPDEFINES = ['BUILD_FOR_FAH'])

    libname = 'protomol-fah'
    libsrc.append('#/protomol/modules.cpp')

# Build
for s in subsrc:
    libsrc.append('#/protomol/' + s)


# Build libprotomol
env.Library(libname, libsrc)


if not fah:
    # Build Protomol
    src = ['protomol/main.cpp', 'protomol/modules.cpp']
    protomol = env.Program('ProtoMol', src)
    Default(protomol)


# Tidy
env.Command('tidy', '', 'rm -f config.log $$(find . -name \*~ -o -name \#*)')
